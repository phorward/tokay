# Tokay default prelude

Repeat : @<P> min=1, max=void {
    begin {
        res = list()
    }

    P {
        res += $1
        if !max || res.len < max repeat
    }

    if !res || res.len < min reject
    res
}

Pos : @<P>{ Repeat<P> }
Kle : @<P>{ Repeat<P>(min=0) }
Opt : @<P>{ P | Void }

List : @<P, Separator: (',' _), empty: true> {
    Self Separator P   $1 + $3
    if empty (Self Separator)   # allows for trailing Separator
    P  ($1, )
}

Not : @<P> {
    P reject
    Void
}

Peek : @<P> {
    P reset
}

Expect : @<P> msg=void {
    accept P
    error(msg || "Expecting " + *P + ", but got " + repr((Token | Char)))
}

Number : Float | Int
Token : Word | Number | AsciiPunctuation
